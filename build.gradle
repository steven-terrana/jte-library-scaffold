plugins{
  // used to packate these libraries as a jenkins plugin
  id "io.jenkins.jte" version "0.2.0"
  // used to execute unit test suite 
  id 'groovy'
}

// packaging info
jenkinsPlugin{
  jenkinsVersion = "2.346.2"
  shortName = "jte-library-scaffold"
  // will be the value in the drop down in Jenkins UI
  displayName = "JTE Library Scaffold"
}

repositories {
    mavenCentral()
    maven { url "https://repo.jenkins-ci.org/public/" }
    maven { url "https://repo.jenkins-ci.org/releases" }
    maven { url "https://repo.maven.apache.org/maven2" }
}

// an artifact required by how Jenkins Spock is hard-coded
project.buildDir = "target" 

// determine test files
def tests = [ "test" ]
fileTree("libraries").visit{ FileVisitDetails details ->
  if(details.file.isDirectory() && details.file.path ==~ ".*libraries/.*/test"){
    tests << details.file.path
  }
}

// don't compile libraries
compileGroovy.enabled = false 

sourceSets{
  main{
    groovy{
      srcDirs = [ "libraries" ]
    }
  }
  test{
    output.resourcesDir("${buildDir}/test-classes")
    groovy{
      srcDirs = tests
    }
    resources{
      srcDirs = [ "libraries" ]
    }
  }
}

dependencies {
    // unit test framework deps
    implementation group: 'com.homeaway.devtools.jenkins', name: 'jenkins-spock', version: '2.1.5'
    implementation group: 'javax.servlet', name: 'javax.servlet-api', version:'3.1.0'
    implementation group: 'junit', name: 'junit', version:'4.12'
    implementation group: 'org.codehaus.groovy', name: 'groovy-all', version:'2.4.11'
    implementation group: 'org.spockframework', name: 'spock-core', version:'1.1-groovy-2.4'

    // our plugin deps
    implementation group: 'org.jenkins-ci.plugins.workflow', name: 'workflow-step-api', version:'2.10'
    implementation group: 'org.jenkins-ci.plugins.workflow', name: 'workflow-cps', version:'2.36'
    implementation group: 'org.jenkins-ci.plugins.workflow', name: 'workflow-scm-step', version:'2.7'
    implementation group: 'org.jenkins-ci.plugins.workflow', name: 'workflow-durable-task-step', version:'2.21'
    implementation group: "org.jenkins-ci.plugins.workflow", name: "workflow-basic-steps", version: "2.9"
    implementation group: 'org.jenkins-ci.plugins', name: 'pipeline-stage-step', version:'2.3'
    implementation group: 'org.jenkins-ci.plugins', name: 'ssh-agent', version:'1.16'
    implementation group: "org.jenkins-ci.plugins", name: "pipeline-utility-steps", version: "2.1.0"
    implementation group: "org.jenkins-ci.plugins", name: "credentials-binding", version: "1.16"

    //our plugin
    implementation group: 'org.jenkins-ci.plugins', name: "templating-engine", version: "2.2.2"
 }
